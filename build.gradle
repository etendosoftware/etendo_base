plugins {
    id 'maven-publish'
    id 'java'
}

ant.importBuild('build.xml') { String oldTargetName ->
    switch (oldTargetName) {
        case 'clean':
            return 'antClean'
        case 'setup':
            return 'antSetup'
        case 'init':
            return 'antInit'
        case 'install.source':
            return 'antInstall'
        default:
            return oldTargetName
    }
}

repositories {
    jcenter()
    maven {
        url "https://repo.futit.cloud/repository/maven-releases"
    }
    /*maven {
        url "http://localhost:8081/repository/obclassic"
    }*/
}

configurations {
    coreDep
    moduleDeps
}

dependencies {
    coreDep 'com.smf.classic.core:ob:[20.1.2,)@zip'
    //moduleDeps 'com.smf:securewebservices:1.0.1@zip'
}

/** Expand the core from the dependency */
task expandCore(type: Copy) {
    def extractDir = "${project.projectDir}"

    dependsOn configurations.coreDep
    from {
        configurations.coreDep.collect {
            zipTree(it)
        }
    }
    into extractDir
}

/** Expand modules from the dependencies */
task expandModules(type: Copy) {
    def extractDir = "${project.projectDir}/modules"
    dependsOn configurations.moduleDeps
    from {
        configurations.moduleDeps.collect {
            zipTree(it)
        }
    }
    into extractDir
}



/** Copy Openbravo.properties template and set values */
task prepareConfig(type: Copy){
    from file("config/Openbravo.properties.template")
    into file("config")
    rename { String fileName ->
        fileName.replace("Openbravo.properties.template", "Openbravo.properties")
    }
    doFirst{
        ant.propertyfile(file: "config/Openbravo.properties") {
            entry( key: "bbdd.sid", value: findProperty("bbdd.sid"))
            entry( key: "bbdd.url", value: "jdbc:postgresql://localhost:"+findProperty("bbdd.port"))
            entry( key: "bbdd.systemUser", value: findProperty("bbdd.systemUser"))
            entry( key: "bbdd.systemPassword", value: findProperty("bbdd.systemPassword"))
            entry( key: "bbdd.user", value: findProperty("bbdd.user"))
            entry( key: "bbdd.password", value: findProperty("bbdd.password"))
        }
    }
}

/** Expand core and modules from the dependencies and set properties */
task expand{
    dependsOn expandCore
    dependsOn expandModules
    tasks.findByName('expandModules').mustRunAfter 'expandCore'
    finalizedBy prepareConfig
}

/** Call ant setup to prepare enviroment */
task setup {
    ant.properties['nonInteractive'] = true
    ant.properties['acceptLicense'] = true
    finalizedBy antSetup
}

/** The install.source ant task now depends on ant setup */
task install{
    dependsOn setup
    finalizedBy antInstall
}